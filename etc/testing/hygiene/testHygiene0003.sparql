prefix owl:   <http://www.w3.org/2002/07/owl#> 
prefix rdf:   <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
prefix xsd:   <http://www.w3.org/2001/XMLSchema#> 
prefix rdfs:  <http://www.w3.org/2000/01/rdf-schema#> 
prefix afn: <http://jena.apache.org/ARQ/function#>


##
# banner Subproperties ranges should not cross

SELECT DISTINCT ?error
WHERE {
  ?p1 rdfs:range ?D1 . 
  ?p1 rdfs:subPropertyOf ?p2 .
  ?p2 rdfs:range ?D2 .
  ?D2 rdfs:subClassOf+ ?D1 .
  FILTER NOT EXISTS {?p1 owl:deprecated "true"^^xsd:boolean} .
  FILTER NOT EXISTS {?p2 owl:deprecated "true"^^xsd:boolean} .
   BIND (concat ("PRODERROR: Crossed ranges. ", afn:localname(?p1), " < ", afn:localname (?p2), " but the range ", afn:localname (?D1), " is a superclass of ", afn:localname (?D2)) AS ?error)
}
